PREFIX prokino: <http://om.cs.uga.edu/prokino/2.0/#>
PREFIX prokino.rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX pro.oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX pro.owl: <http://www.w3.org/2002/07/owl#>
PREFIX pro.paf: <http://pir.georgetown.edu/pro/paf#>
PREFIX pro.pr: <http://purl.obolibrary.org/obo/pr#>
PREFIX pro.rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX pro.rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nextprot: <http://nextprot.org/rdf#>
PREFIX nextprot.db: <http://nextprot.org/rdf/db/>
PREFIX nextprot.rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mgi: <http://bio2rdf.org/mgi_vocabulary:>
PREFIX mgi.affy: <http://bio2rdf.org/affymetrix_vocabulary:>
PREFIX mgi.dcterms: <http://purl.org/dc/terms/>
PREFIX mgi.go: <http://bio2rdf.org/goa_vocabulary:>
PREFIX mgi.owl: <http://www.w3.org/2002/07/owl#>
PREFIX mgi.rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT
	STR(?genename) AS ?Gene
	STR(?uniprotid) AS ?UniProt_ID
	STR(?nextprot_entry) AS ?neXtProt_Entry
	CONCAT(STR(?wt),STR(?pos_start)) AS ?Position
	STR(?domain_name) AS ?Domain
	STR(?mt) AS ?Mutant_Type
	STR(?comment) AS ?Description
	STR(?assigned_by) AS ?Source
	STR(?accession) AS ?Source_ID
WHERE
{
	SERVICE <https://api.nextprot.org/sparql>
	{
		SELECT DISTINCT
			?genename
			?uniprotid
			STRAFTER(STR(?nextprotentry),"/entry/") AS ?nextprot_entry
			?wt
			?pos_start
			?domain_name
			?mt
			?comment
			STRAFTER(STR(?assignedby),"/source/") AS ?assigned_by
			?accession
		WHERE
		{
			#Protein kinase UniProtID
			{
				SELECT DISTINCT
					?genename
					?uniprotid
					?nextprotentry
				WHERE
				{
					SERVICE <http://vulcan.cs.uga.edu/sparql>
					{
						SELECT DISTINCT
							?genename
							?uniprotid
							CONCAT("http://nextprot.org/rdf/entry/NX_",STR(?uniprotid)) AS ?nextprotentry
						WHERE
						{
							#Define the gene of interest
							VALUES ?query_gene {'EGFR'}
							
							?gene prokino.rdf:type prokino:Gene .
							?gene prokino:presentIn prokino:Human .
							?gene prokino:hasPrimaryName ?genename .
							?gene prokino:hasDbXref ?protid .
							FILTER(STR(?genename) = ?query_gene)
							
							?protid prokino:isPrimaryUniprotId ?isprimaryuniprotid .
							?protid prokino:hasIdentifier ?uniprotid .
							FILTER REGEX(?isprimaryuniprotid,"true")
						}
					}
				}
			}
			#neXtProt: Mutation
			OPTIONAL
			{
				SELECT DISTINCT
					?nextprotentry
					?wt
					?pos_start
					?domain_name
					?mt
					?comment
					?assignedby
					?accession
				WHERE
				{
					#Define mutation position
					VALUES ?query_position {'768'}
					
					?nextprotentry nextprot:isoform ?iso .
					?iso nextprot:variant ?variant .
					?iso nextprot:domain ?domain .
					
					?variant nextprot:original ?wt .
					?variant nextprot:start ?pos_start .
					?variant nextprot:end ?pos_end .
					?variant nextprot:variation ?mt .
					?variant nextprot:evidence ?evidence .
					OPTIONAL
					{
						?variant nextprot.rdfs:comment ?comment .
					}
					
					?domain nextprot:start ?domain_start .
					?domain nextprot:end ?domain_end .
					?domain nextprot.rdfs:comment ?domain_name .
					FILTER(STR(?query_position) >= STR(?domain_start))
					FILTER(STR(?query_position) <= STR(?domain_end))
					
					?evidence nextprot:assignedBy ?assignedby .
					?evidence nextprot:reference ?reference .
					?reference nextprot:accession ?accession .
					
					#Missense substitution
					FILTER(?pos_start = ?pos_end)
					FILTER(STR(?pos_start) = ?query_position)
				}
			}
		}
	}
}
ORDER BY ?genename ?mt ?accession