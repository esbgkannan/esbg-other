PREFIX prokino: <http://om.cs.uga.edu/prokino/2.0/#>
PREFIX prokino.rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX pro.oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX pro.owl: <http://www.w3.org/2002/07/owl#>
PREFIX pro.paf: <http://pir.georgetown.edu/pro/paf#>
PREFIX pro.pr: <http://purl.obolibrary.org/obo/pr#>
PREFIX pro.rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX pro.rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nextprot: <http://nextprot.org/rdf#>
PREFIX nextprot.db: <http://nextprot.org/rdf/db/>
PREFIX nextprot.rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mgi: <http://bio2rdf.org/mgi_vocabulary:>
PREFIX mgi.affy: <http://bio2rdf.org/affymetrix_vocabulary:>
PREFIX mgi.dcterms: <http://purl.org/dc/terms/>
PREFIX mgi.go: <http://bio2rdf.org/goa_vocabulary:>
PREFIX mgi.owl: <http://www.w3.org/2002/07/owl#>
PREFIX mgi.rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT
	STR(?genename) AS ?Gene
	STR(?uniprotid) AS ?UniProt_ID
	STR(?nextprot_entry) AS ?neXtProt_ID
	STR(?accession) AS ?Pathway_ID
	STR(?pathway_name) AS ?Pathway_Name
WHERE
{
	SERVICE <https://api.nextprot.org/sparql>
	{
		SELECT DISTINCT
			?genename
			?uniprotid
			STRAFTER(STR(?nextprotentry),"/entry/") AS ?nextprot_entry
			?accession
			?pathway_name
		WHERE
		{
			#Protein kinase UniProtID
			{
				SELECT DISTINCT
					?genename
					?uniprotid
					?nextprotentry
				WHERE
				{
					SERVICE <http://vulcan.cs.uga.edu/sparql>
					{
						SELECT DISTINCT
							?genename
							?uniprotid
							CONCAT("http://nextprot.org/rdf/entry/NX_",STR(?uniprotid)) AS ?nextprotentry
						WHERE
						{
							#Define the gene of interest
							VALUES ?query_genes {'EGFR' 'PKCB' 'PKCT'}
							
							?gene prokino.rdf:type prokino:Gene .
							?gene prokino:presentIn prokino:Human .
							?gene prokino:hasPrimaryName ?genename .
							?gene prokino:hasDbXref ?protid .
							FILTER(STR(?genename) = ?query_genes)
							
							?protid prokino:isPrimaryUniprotId ?isprimaryuniprotid .
							?protid prokino:hasIdentifier ?uniprotid .
							FILTER REGEX(?isprimaryuniprotid,"true")
						}
					}
				}
			}
			#neXtProt: Pathway
			OPTIONAL
			{
				SELECT DISTINCT
					?nextprotentry
					?accession
					?pathway_name
				WHERE
				{
					?nextprotentry nextprot:isoform ?iso .
					?iso nextprot:pathway ?pathway .
					?pathway nextprot.rdfs:comment ?pathway_name .
					?pathway nextprot:evidence ?evidence .
					?evidence nextprot:reference ?xref .
					{
						?xref nextprot:provenance nextprot.db:KEGGPathway .
					}
					UNION
					{
						?xref nextprot:provenance nextprot.db:Reactome .
					}
					?xref nextprot:accession ?accession .
				}
			}
		}
	}
}
ORDER BY ?genename ?pathway_name