PREFIX prokino: <http://om.cs.uga.edu/prokino/2.0/#>
PREFIX prokino.rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX pro.oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX pro.owl: <http://www.w3.org/2002/07/owl#>
PREFIX pro.paf: <http://pir.georgetown.edu/pro/paf#>
PREFIX pro.pr: <http://purl.obolibrary.org/obo/pr#>
PREFIX pro.rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX pro.rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nextprot: <http://nextprot.org/rdf#>
PREFIX nextprot.db: <http://nextprot.org/rdf/db/>
PREFIX nextprot.rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mgi: <http://bio2rdf.org/mgi_vocabulary:>
PREFIX mgi.affy: <http://bio2rdf.org/affymetrix_vocabulary:>
PREFIX mgi.dcterms: <http://purl.org/dc/terms/>
PREFIX mgi.go: <http://bio2rdf.org/goa_vocabulary:>
PREFIX mgi.owl: <http://www.w3.org/2002/07/owl#>
PREFIX mgi.rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT
	STR(?genename) AS ?Gene
	STR(?uniprotid) AS ?Mouse_UniProt_ID
	STRAFTER(STR(?phenotype_id),"/bio2rdf.org/") AS ?Phenotype_id
	STR(?phenotype) AS ?Phenotype
WHERE
{
	SERVICE <http://bio2rdf.org/sparql>
	{
		#Get Mouse UniProtID
		{
			SELECT DISTINCT
				?genename
				?uniprotid
				STR(?mgi_uniprot) AS ?mgi_uniprotid
			WHERE
			{
				SERVICE <http://vulcan.cs.uga.edu/sparql>
				{
					SELECT DISTINCT
						?genename
						?uniprotid
						CONCAT("http://bio2rdf.org/uniprot:",STR(?uniprotid)) AS ?mgi_uniprot
					WHERE
					{
						#Define the gene of interest
						VALUES ?query_genes {'EGFR' 'PKCB' 'PKCT'}
						
						?gene prokino.rdf:type prokino:Gene .
						?gene prokino:presentIn prokino:Mouse .
						?gene prokino:hasPrimaryName ?genename .
						?gene prokino:hasDbXref ?protid .
						FILTER(LCASE(STR(?genename)) = LCASE(STR(?query_genes)))
						
						?protid prokino:isPrimaryUniprotId ?isprimaryuniprotid .
						?protid prokino:hasIdentifier ?uniprotid .
						FILTER REGEX(?isprimaryuniprotid,"true")
					}
				}
			}
		}
		#MGI: Get MGI ID & Phenotype
		OPTIONAL
		{
			SELECT DISTINCT
				?mgi_gene
				STR(?mgi_uniprot) AS ?mgi_uniprotid
				?phenotype_id
				?phenotype
			WHERE
			{
				?mgi_gene a mgi:Gene .
				?mgi_gene mgi:x-uniprot ?mgi_uniprot .
				?mgi_model mgi:marker ?mgi_gene .
				?mgi_model mgi:phenotype ?phenotype_id .
				?owl_class mgi.owl:sameAs ?phenotype_id .
				?owl_class mgi.rdfs:label ?phenotype
			}
		}
	}
}
ORDER BY ?genename ?Phenotype